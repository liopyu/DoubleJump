plugins {
    id 'java'
    id 'org.jetbrains.gradle.plugin.idea-ext' version '1.3'
}

ext {
    hytaleHome = "${System.getProperty("user.home")}/AppData/Roaming/Hytale"
    neoTaleVersion = "0.0.1"
}

java {
    toolchain.languageVersion = JavaLanguageVersion.of(java_version)
}

javadoc {
    options.addStringOption('Xdoclint:-missing', '-quiet')
}
repositories {
    maven { url 'https://dl.cloudsmith.io/public/lio/neotale/maven/' }
}
dependencies {
    compileOnly(files("$hytaleHome/install/$patchline/package/game/latest/Server/HytaleServer.jar"))
    implementation "net.liopyu:neotale:${neoTaleVersion}"
}

def serverRunDir = file("$projectDir/run")
serverRunDir.mkdirs()

tasks.register('updatePluginManifest') {
    def manifestFile = file('src/main/resources/manifest.json')
    doLast {
        if (!manifestFile.exists()) {
            throw new GradleException("Could not find manifest.json at ${manifestFile.path}!")
        }
        def manifestJson = new groovy.json.JsonSlurper().parseText(manifestFile.text)
        manifestJson.Version = version
        manifestJson.IncludesAssetPack = includes_pack.toBoolean()
        manifestFile.text = groovy.json.JsonOutput.prettyPrint(groovy.json.JsonOutput.toJson(manifestJson))
    }
}

tasks.named('processResources') {
    dependsOn 'updatePluginManifest'
}

def hotswapModDir = file("$buildDir/hotswap-mod")

tasks.register('syncHotswapMod', Sync) {
    dependsOn 'classes'
    into hotswapModDir
    from(sourceSets.main.output)

    from({
        configurations.runtimeClasspath
                .findAll { it.name.endsWith('.jar') }
                .findAll { !it.name.equalsIgnoreCase('HytaleServer.jar') }
                .collect { zipTree(it) }
    })
}

idea.project.settings.runConfigurations {
    'HytaleServer'(org.jetbrains.gradle.ext.Application) {
        mainClass = 'com.hypixel.hytale.Main'
        moduleName = project.idea.module.name + '.main'

        def serverJar = file("$hytaleHome/install/$patchline/package/game/latest/Server/HytaleServer.jar").absolutePath
        def assetsZip = file("$hytaleHome/install/$patchline/package/game/latest/Assets.zip").absolutePath
        def hotswapDir = hotswapModDir.absolutePath

        jvmArgs = "-XX:AOTCache=HytaleServer.aot -Xms4G -Xmx10G -cp \"${serverJar}\""

        programParameters =
                "--allow-op " +
                        "--assets \"${assetsZip}\" " +
                        "--backup --backup-dir backup --backup-frequency 720 " +
                        "--mods \"${hotswapDir}\" "

        if (load_user_mods.toBoolean()) {
            programParameters += "--mods \"$hytaleHome/UserData/Mods\" "
        }

        workingDirectory = serverRunDir.absolutePath
    }
}
